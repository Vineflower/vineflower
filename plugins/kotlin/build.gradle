plugins {
  id 'com.github.johnrengelman.shadow'
  id 'java'
  id 'org.jetbrains.kotlin.jvm'
}

apply plugin: 'maven-publish'

group = 'org.quiltmc'
version = "0.1.0"
archivesBaseName = 'quiltflower-kotlin'

repositories {
  mavenCentral()
}

compileJava {
  sourceCompatibility '11'
  targetCompatibility '11'
}


sourceSets {
  testDataKotlin.kotlin.srcDirs files("testData/src/kt/")
}

dependencies {
  implementation project(":")
  shadow fileTree(dir: 'libs', include: ['*.jar'])

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
  testImplementation testFixtures(project(":"))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

  testDataKotlinImplementation platform('org.jetbrains.kotlin:kotlin-bom')
  testDataKotlinImplementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  testRuntimeOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

java.toolchain {
  languageVersion = JavaLanguageVersion.of(11)
}

task testDataClasses {
  group = 'build'
}
testClasses.dependsOn(testDataClasses)

compileTestDataKotlinKotlin {
  destinationDirectory = file("testData/classes/kt")
}
testDataClasses.dependsOn(testDataKotlinClasses)

test {
  useJUnitPlatform()
}

build.dependsOn(shadowJar)

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "quiltflower-kotlin"
      from components.java
    }
  }
}