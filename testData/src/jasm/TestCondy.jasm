package pkg;

super public class TestCondy
	version 55:0
{

public static final Field field:"Ljava/lang/Integer;";
public static final Field selfField:"Lpkg/TestCondy;";

private static Method "<clinit>":"()V"
  stack 1 locals 0
{
  iconst_1;
  invokestatic Method java/lang/Integer."valueOf":"(I)Ljava/lang/Integer;";
  putstatic Field field:"Ljava/lang/Integer;";

  aconst_null;
  putstatic Field selfField:"Lpkg/TestCondy;";
}

public static Method testNull:"()Ljava/lang/String;"
	stack 1 locals 0
{
  ldc Dynamic REF_invokeStatic:java/lang/invoke/ConstantBootstraps.nullConstant:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;)Ljava/lang/Object;":null:"Ljava/lang/String;";
  areturn;
}

public static Method testEnum:"()Lpkg/FakeEnum;"
	stack 1 locals 0
{
  ldc Dynamic REF_invokeStatic:java/lang/invoke/ConstantBootstraps.enumConstant:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;)Ljava/lang/Enum;":FOO:"Lpkg/FakeEnum;";
  areturn;
}

public static Method testPrimitiveClass:"()Ljava/lang/Class;"
	stack 1 locals 0
{
  ldc Dynamic REF_invokeStatic:java/lang/invoke/ConstantBootstraps.primitiveClass:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;)Ljava/lang/Class;":V:"Ljava/lang/Class;";
  areturn;
}

public static Method testWrongPrimitiveClass:"()Ljava/lang/Class;"
	stack 1 locals 0
{
  ldc Dynamic REF_invokeStatic:java/lang/invoke/ConstantBootstraps.primitiveClass:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;)Ljava/lang/Class;":U:"Ljava/lang/Class;";
  areturn;
}

public static Method testGetStaticFinal:"()Ljava/lang/Class;"
	stack 1 locals 0
{
  ldc Dynamic REF_invokeStatic:java/lang/invoke/ConstantBootstraps.getStaticFinal:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;Ljava/lang/Class;)Ljava/lang/Object;":field:"Ljava/lang/Integer;"{
    class "pkg/TestCondy"
  };
  areturn;
}

public static Method testGetSelfStaticFinal:"()Ljava/lang/Class;"
	stack 1 locals 0
{
  ldc Dynamic REF_invokeStatic:java/lang/invoke/ConstantBootstraps.getStaticFinal:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;Ljava/lang/Class;)Ljava/lang/Object;":selfField:"Lpkg/TestCondy;";
  areturn;
}

public static Method testStaticVarHandle:"()Ljava/lang/invoke/VarHandle;"
	stack 1 locals 0
{
  ldc Dynamic REF_invokeStatic:java/lang/invoke/ConstantBootstraps.staticFieldVarHandle:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;Ljava/lang/Class;)Ljava/lang/Object;":field:"Ljava/lang/invoke/VarHandle;"{
    class "pkg/TestCondy",
    class "java/lang/Integer"
  };
  areturn;
}

public static Method "test":"(I)Ljava/nio/file/StandardOpenOption;"
  stack 2 locals 1
{
  ldc Dynamic REF_invokeStatic:
    Method pkg/TestConstantDynamic.getValues:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;)[Ljava/lang/Object;":
    EnumC$Values:
    "[Ljava/nio/file/StandardOpenOption;";
  iload 0;
  aaload;
  areturn;
}

public static Method getValues:"(Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/Class;)[Ljava/nio/file/StandardOpenOption;"
  stack 1 locals 3
{
  invokestatic Method java/nio/file/StandardOpenOption.values:"()[Ljava/nio/file/StandardOpenOption;";
  areturn;
}
}